#@TYPE: Machine
#@NAME: SolidRun RZ/G2LC SYstem on Module
#@DESCRIPTION: Machine configuration for SolidRun RZ/G2LC SoM on HummingBoard Ripple

require conf/machine/include/soc-family.inc
SOC_FAMILY = "rzg2l-family:mali-family"
require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc
require conf/machine/include/rz-common.inc

# Machine configuration
MACHINE_FEATURES = "opengles opencl mali-g31 bluetooth rtc serial usbgadget usbhost wifi"

IMAGE_FSTYPES:append = " tar.bz2 ext4"

# Configuration for ARM Trusted Firmware
EXTRA_IMAGEDEPENDS += " trusted-firmware-a"

# flash writer
EXTRA_IMAGEDEPENDS += " flash-writer"

# Firmware-pack
EXTRA_IMAGEDEPENDS += " firmware-pack"

# Linux kernel configuration
PREFERRED_PROVIDER_virtual/kernel ?= "linux-renesas"
PREFERRED_VERSION_linux-renesas ?= "6.1%"
KERNEL_IMAGETYPE ?= "Image.gz"
KERNEL_DEVICETREE = " \
	renesas/${SR_MMC_OVERLAY} \
	renesas/${SR_SD_OVERLAY} \
	renesas/r9a07g044c2-hummingboard-iiot.dtb \
	renesas/r9a07g044c2-hummingboard-ripple.dtb \
	renesas/rzg2lc-hummingboard-pulse-leds.dtbo \
"

# SolidRun eMMC-/SD-Mux Overlays
SR_MMC_OVERLAY ?= "rzg2l-solidrun-mmc-overlay.dtbo"
SR_SD_OVERLAY ?= "rzg2l-solidrun-sd-overlay.dtbo"

# Trusted Firmware-A configuration
PREFERRED_VERSION_trusted-firmware-a ?= "2.9%"
TFA_PLATFORM ?= "g2l"
TFA_BOARD ?= "sr_rzg2lc"
TFA_UBOOT ?= "1"
TFA_BUILD_TARGET ?= "bl2 bl31 fip"
BL2_BOOT_TARGET ?= "mmc esd"
BL2_ADJUST_VMA ?= "0x00011E00"
FIP_ADJUST_VMA ?= "0x0000"

# U-Boot Configuration
UBOOT_CONFIG ??= "rzg2lc-hummingboard"
UBOOT_CONFIG[rzg2lc-hummingboard] = "rzg2lc-solidrun_defconfig"
PREFERRED_VERSION_u-boot ?= "2021.10%"

# Flash Writer configuration
FLASH_WRITER_BOARD ?= "RZG2LC_HUMMINGBOARD"

# Wic image
WKS_FILE = "rz-image-bootpart-esd-sr.wks.in"

# install kernel to "Image" filename because wic image extlinux.conf generator does not recognise Image.gz.
# U-Boot decompresses it fine regardless
IMAGE_BOOT_FILES:append = " ${KERNEL_IMAGETYPE};Image "

# install DTBs with renesas/ prefix
IMAGE_BOOT_FILES:append = " \
	r9a07g044c2-hummingboard-iiot.dtb;renesas/r9a07g044c2-hummingboard-iiot.dtb \
	r9a07g044c2-hummingboard-ripple.dtb;renesas/r9a07g044c2-hummingboard-ripple.dtb \
	rzg2lc-hummingboard-pulse-leds.dtbo;renesas/rzg2lc-hummingboard-pulse-leds.dtbo \
"
# HACK: place one sentinel dtb without renesas/ prefix to fix wic image generated extlinux.conf fdtdir path
IMAGE_BOOT_FILES:prepend = " r9a07g044c2-hummingboard-ripple.dtb;sentinel.dtb "

do_image_wic[depends] += "firmware-pack:do_deploy flash-writer:do_deploy"

# include drivers & support packages
# RZ/G2LC System on Module
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:append = " \
	kernel-module-brcmfmac \
	kernel-module-brcmutil \
	kernel-module-btbcm \
	kernel-module-cfg80211 \
	kernel-module-hci-uart \
	linux-firmware-cyw43439-sr \
"
# RZ/G2LC SoM Murata out-of-tree driver
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:append = " kernel-module-murata-cyw-fmac "
# HummingBoard Ripple
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:append = " \
	kernel-module-userspace-consumer \
"
