#
# Configuration shared between RZ/G2 Series SoMs:
# - RZ/G2LC SoM
# - RZ/G2L SoM
# - RZ/V2L SoM
#

require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc
require conf/machine/include/rz-common.inc
require conf/machine/include/rzg2l-family.inc
MACHINEOVERRIDES =. "rzg2l-sr-som-common:"

SERIAL_CONSOLES ?= "115200;ttySC0"

# Machine configuration
MACHINE_FEATURES = "opengles opencl mali-g31 bluetooth rtc serial usbgadget usbhost wifi"

IMAGE_FSTYPES:append = " tar.bz2 ext4"

# Configuration for ARM Trusted Firmware
EXTRA_IMAGEDEPENDS += " trusted-firmware-a"

# flash writer
EXTRA_IMAGEDEPENDS += " flash-writer"

# Firmware-pack
EXTRA_IMAGEDEPENDS += " firmware-pack"

# Linux kernel configuration
KERNEL_IMAGETYPE = "Image.gz"

# Trusted Firmware-A configuration
PREFERRED_VERSION_trusted-firmware-a ?= "2.9%"
TFA_UBOOT ?= "1"
TFA_BUILD_TARGET ?= "bl2 bl31 fip"

BL2_BASE_ADDR ?= "0x00012000"
BL2_BOOT_TARGET ?= "mmc esd"
BL2_ADJUST_VMA ?= "0x00011E00"
FIP_ADJUST_VMA ?= "0x0000"

# SolidRun eMMC-/SD-Mux Overlays
SR_MMC_OVERLAY ?= "rzg2l-solidrun-mmc-overlay.dtbo"
SR_SD_OVERLAY ?= "rzg2l-solidrun-sd-overlay.dtbo"

# U-Boot Configuration
UBOOT_CONFIG[rzg2ul-hummingboard] = "rzg2ul-solidrun_defconfig"
UBOOT_CONFIG[rzg2lc-hummingboard] = "rzg2lc-solidrun_defconfig"
UBOOT_CONFIG[rzg2l-hummingboard] = "rzg2l-solidrun_defconfig"
UBOOT_CONFIG[rzv2l-hummingboard] = "rzv2l-solidrun_defconfig"
PREFERRED_VERSION_u-boot ?= "2021.10%"

# Wic image
FIP_WIC_OFFSET = "--offset 128s"
WKS_FILE = "rz-image-bootpart-esd-sr.wks.in"

# install kernel to "Image" filename because wic image extlinux.conf generator does not recognise Image.gz.
# U-Boot decompresses it fine regardless
IMAGE_BOOT_FILES:append = " ${KERNEL_IMAGETYPE};Image "

do_image_wic[depends] += "firmware-pack:do_deploy flash-writer:do_deploy"

# common RZ/G2LC, RZ/G2L, RZ/V2L boot files
IMAGE_BOOT_FILES = " \
	bl2_bp_mmc-${MACHINE}.bin \
	fip-${MACHINE}.bin \
	bl2_bp_mmc-${MACHINE}.srec \
	fip-${MACHINE}.srec \
	Flash_Writer_SCIF_${FLASH_WRITER_BOARD}_DDR4_512MB_1PCS.mot \
	Flash_Writer_SCIF_${FLASH_WRITER_BOARD}_DDR4_1GB_1PCS.mot \
	Flash_Writer_SCIF_${FLASH_WRITER_BOARD}_DDR4_2GB_1PCS.mot \
"

# include drivers & support packages
# RZ/G2LC System on Module
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:append = " \
	kernel-module-brcmfmac \
	kernel-module-brcmutil \
	kernel-module-btbcm \
	kernel-module-cfg80211 \
	kernel-module-hci-uart \
"
# Murata out-of-tree driver
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:append = " kernel-module-murata-cyw-fmac "
# HummingBoard Ripple
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:append = " \
	kernel-module-userspace-consumer \
"
